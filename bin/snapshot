#!/bin/zsh
setopt noclobber extendedglob

task() { print -rP "%B%F{blue}::%f $1%b" }
error() { print -rP "%B%F{red}error:%f%b $1" }

cd -q -- ${TMP:-/tmp}

readonly file=$(date +snapshot-%Y%b%d%a.tar.zst)

USER=${SUDO_USER:-$USER}
HOME=/home/$USER

{
	task 'Preparing...'
	pacman -Qqen >pkglist.txt &
	pacman -Qqem >localpkglist.txt &
	wait

	task 'Collecting files...'
	umask 077
	tar -cavPf $file \
		--exclude='**/.git/'{objects,refs,logs}'/**' \
		--exclude='**/gnupg/S.*' \
		-T - <<FILES
$(print -l $HOME/*~*/(mem|mus|var) $HOME/.*(^/))
$HOME/.config
$HOME/.mozilla

pkglist.txt
localpkglist.txt
/etc/autofs
/etc/hostname
/etc/iptables/
/etc/kmscon
/etc/mkinitcpio.conf
/etc/modprobe.d/
/etc/netctl/
/etc/pacman.d/hooks
/etc/pacman.d/mirrorlist
$(pacman -Qii | awk '$1=="MODIFIED" {print $2}')
FILES
} always {
	if ((TRY_BLOCK_ERROR==0)); then
		task "Snapshot taken: $PWD/$file."
		chown $USER $file
		chmod 400 $file
	else
		error "Failed to create complete snapshot."
	fi
	rm -vf {local,}pkglist.txt
}
