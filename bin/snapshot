#!/bin/zsh
setopt noclobber

task() { print -rP "%B%F{blue}::%f $1%b" }
error() { print -rP "%B%F{red}error:%f%b $1" }

cd -q -- ${TMP:-/tmp}

readonly FILE=$(date +snapshot-%Y%b%d%a.tar.xz)

{
  task 'Preparing...'
  pacman -Qqen >pkglist.txt &
  pacman -Qqem >localpkglist.txt &
  wait

  task 'Collecting files...'
  umask 077
  tar cavPf $FILE \
    --exclude='**/.git/'{objects,refs,logs}'/**' \
    --exclude='**/gnupg/S.*' \
    -T - <<FILES
/home/${SUDO_USER:-$USER}/pix
/home/${SUDO_USER:-$USER}/doc
/home/${SUDO_USER:-$USER}/bin
/home/${SUDO_USER:-$USER}/.config
/home/${SUDO_USER:-$USER}/.mozilla

/home/${SUDO_USER:-$USER}/.radare2rc
/home/${SUDO_USER:-$USER}/.dir_colors
/home/${SUDO_USER:-$USER}/.mailcap
/home/${SUDO_USER:-$USER}/.mbsyncrc
/home/${SUDO_USER:-$USER}/.passss
/home/${SUDO_USER:-$USER}/.tmux.conf
/home/${SUDO_USER:-$USER}/.urlview
/home/${SUDO_USER:-$USER}/.XCompose
/home/${SUDO_USER:-$USER}/.Xdefaults
/home/${SUDO_USER:-$USER}/.zshenv

pkglist.txt
localpkglist.txt
/etc/autofs
/etc/hostname
/etc/iptables/
/etc/kmscon
/etc/mkinitcpio.conf
/etc/modprobe.d/
/etc/netctl/
/etc/pacman.d/hooks
/etc/pacman.d/mirrorlist
$(pacman -Qii | awk '$1=="MODIFIED" {print $2}')
FILES
} always {
  if ((TRY_BLOCK_ERROR==0)); then
    task "Snapshot taken: $PWD/$FILE."
    chown ${SUDO_USER:-$USER} $FILE
    chmod 400 $FILE
  else
    error "Failed to create complete snapshot."
  fi
  rm -vf {local,}pkglist.txt
}
