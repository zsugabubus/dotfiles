#autoload

emulate -L zsh

zmodload zsh/datetime
autoload -Uz add-zsh-hook

integer __zmail_last_check

__zmail-check() {
	local unread_mails=( ${MAIL?}/**/new/*(.N) )
	local unread_mailboxes=( ${(S)${(S)unread_mails%$MAIL/}##/new/*} )
	local -A unread
	local mailbox count

	for mailbox in $unread_mailboxes; do
		unread[$mailbox]=$(( unread[$mailbox] + 1 ))
	done

	for mailbox count in ${(kv)unread}; do
		printf "You have %d new mail${${:-s}:$(( count == 1 ))} in %s.\n" $count $mailbox
	done
}

__zmail-precmd() {
	local now=$EPOCHSECONDS
	if (( now - __zmail_last_check < ${MAILCHECK?} )); then
		return
	fi

	# Run at startup only when login shell.
	if (( __zmail_last_check )) || [[ -o login ]]; then
		__zmail-check
	fi

	__zmail_last_check=$now
}

add-zsh-hook precmd __zmail-precmd
